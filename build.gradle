import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer

import java.text.SimpleDateFormat

plugins {
    id 'com.gradleup.shadow' version '8.3.3'
    id 'java'
    id 'io.freefair.lombok' version '8.10'
}

ext {
    project_name = 'SlashFTop'
    project_author = 'TheGeekedGamer'
    project_version = hasProperty('ver') ? ver.toUpperCase().replace("v", "") : getTime() + "-SNAPSHOT"
    project_main_class_name = 'FTopPlugin'
    project_group = 'com.gmail.thegeekedgamer'
    project_artifactId = 'slashftop'
    project_main = project_group + '.' + project_artifactId + '.' + project_main_class_name
}

static def getTime() {
    SimpleDateFormat sdf = new SimpleDateFormat("yyMMdd-HHmm")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    return sdf.format(new Date()).toString()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    maven { name 'papermc'; url 'https://repo.papermc.io/repository/maven-public/' }
    maven { name = 'Rosewood'; url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { name = 'Kangarko'; url = 'https://bitbucket.org/kangarko/libraries/raw/master/' }
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.xenondevs.xyz/releases' }
}

dependencies {
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    implementation 'xyz.xenondevs.invui:invui:1.44'
    implementation 'xyz.xenondevs.invui:inventory-access-r20:1.44:remapped-mojang'
    implementation 'xyz.xenondevs.invui:inventory-access:1.44'
    implementation 'com.github.AvarionMC:yaml:1.1.8'
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'
    compileOnly 'dev.rosewood:rosestacker:1.5.32'
    compileOnly 'org.mineacademy.plugin:FactionsUUID:1.6.9.5-U0.6.40-343'
    compileOnly 'com.github.brcdev-minecraft:shopgui-api:3.0.0'
    compileOnly 'org.mineacademy.plugin:PlaceholderAPI:2.11.6'
    compileOnly 'xyz.xenondevs:particle:1.8.1'
    compileOnly 'com.google.code.gson:gson:2.10'
    compileOnly 'org.apache.commons:commons-lang3:3.12.0'
    compileOnly 'com.zaxxer:HikariCP:6.2.1'
    compileOnly 'com.mysql:mysql-connector-j:9.1.0'
    compileOnly 'com.github.dmulloy2:ProtocolLib:c9ee2f16e2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.1'
    // Use mockito-junit-jupiter for JUnit 5 integration
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    // mockito-core is optional but can be included for additional utilities
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    testImplementation 'dev.rosewood:rosestacker:1.5.32'
    testImplementation 'com.github.MilkBowl:VaultAPI:1.7'
}

tasks.shadowJar {
    archiveFileName = "${project_name}-${project_version}.jar"
    destinationDirectory = file("C:\\Users\\Noah\\Desktop\\1.21.1_Local_Server\\plugins")
    zip64 true

    dependencies {
        include(dependency('xyz.xenondevs.invui:invui-core:1.44'))
        include(dependency('xyz.xenondevs.invui:inventory-access:1.44'))
        include(dependency('xyz.xenondevs.invui:inventory-access-r20:1.44:remapped-mojang'))
        include(dependency('com.zaxxer:HikariCP:6.2.1'))
        include(dependency('com.mysql:mysql-connector-j:9.1.0'))
        include(dependency('com.github.AvarionMC:yaml:1.1.8'))
    }

    // Removed all relocate directives to keep original package structure
    // relocate 'xyz.xenondevs', 'com.gmail.thegeekedgamer.slashftop.shaded.xenondevs'
    // relocate 'com.zaxxer', 'com.gmail.thegeekedgamer.slashftop.shaded.zaxxer'
    // relocate 'com.mysql', 'com.gmail.thegeekedgamer.slashftop.shaded.mysql'
    // relocate 'org.yaml', 'com.gmail.thegeekedgamer.slashftop.shaded.yaml'
    // relocate 'us.mineplex', 'com.gmail.thegeekedgamer.slashftop.shaded.mineplex'

    transform(ServiceFileTransformer) {
        include 'META-INF/services/xyz.xenondevs.inventoryaccess.util.ItemUtils'
    }

    manifest {
        attributes(
                'Implementation-Title': project_name,
                'Implementation-Version': project_version,
                'Built-By': System.properties['user.name'],
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
        )
    }

    doFirst {
        file("${destinationDirectory}/${archiveFileName.get()}").delete()
    }
}

tasks.build {
    dependsOn tasks.shadowJar
}

processResources {
    def props = [
            version    : project_version,
            author     : project_author,
            name       : project_name,
            main       : project_main,
            api_version: '1.21'
    ]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}